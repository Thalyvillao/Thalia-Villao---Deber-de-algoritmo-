// Funciones de usuario para el Algoritmo Menu
// Presenta el menu en la pantalla
Funcion opc = presentarMenu(titulo,opciones,lim) 
	Escribir titulo
	Escribir ""
	Para pos=1 Hasta lim  Hacer
		Escribir opciones[pos]
	Fin Para
	Escribir "Elija opcion[1...",lim,"]: "
	Leer opc
FinFuncion
// f-1 Calcula el proceso matematico de dos numeros dada su operacion
Funcion calculadora(operacion,num1,num2)
	Si operacion = "+" Entonces
		Escribir num1,"+",num2,"=",num1+num2
	SiNo
		Si operacion = "-" Entonces
			Escribir num1,"-",num2,"=",num1-num2
		SiNo
			Si operacion = "*" Entonces
				Escribir num1,"*",num2,"=",num1*num2
			SiNo
				Si operacion = "/" Entonces
					Escribir num1,"/",num2,"=",num1/num2
				SiNo
					Si operacion = "%" Entonces
						Escribir num1,"%",num2,"=",num1%num2
					SiNo
						Si operacion = "^" Entonces
							Escribir num1,"^",num2,"=",num1^num2
						SiNo
							Escribir "Operacion Incorrecta, intentelo de nuevo"
						FinSi
					FinSi
				FinSi
			FinSi
		FinSi
	Fin Si
FinFuncion
// f-2 La despensa "El Baraton", a todas las ventas que pasen de $100,
//se les aplicará un  descuento del 10%, a  todo los demás se les aplicará sólo el 5% 
// luego del recargo del iva del 12%
funcion Descuentos = descuento(iva,ventas)//6
	Si ventas>100 Entonces
		iva=ventas*0.12
		stotal=ventas+iva    
		vpagar=stotal*0.90
		Escribir "El valor a pagar tendra un descuento del 10% y sera: ",vpagar
	SiNo
		iva=ventas*0.12
		stotal=ventas+iva
		vpagar=stotal*0.95
		Escribir "El valor a pagar tendra un descuento del 5% y sera: ",vpagar
	Fin Si
FinFuncion                                                            
//f-3 Calcula y presenta yn rol de pago de acuerdo a los datos que ingrese el usuario
Funcion  Pago_de_sueldo <- pago ( horas,vhora,hextra,hcien,nom )
	sueldo=horas*vhora
	sobret=(hextra*1.5+hcien*2)
	ingreso=sueldo+sobret
	desc=(ingreso*9.35)/100
	nrecibir=ingreso-desc
	Escribir " "
	Escribir "El rol de pago del Sr. ",nom
	Escribir " "
	Escribir "Sueldo por horas trabajadas $ ",sueldo
	Escribir "Sobretiempo de horas al 50% y al 100%: $ ",sobret
	Escribir "Total de ingreso: $ ",ingreso
	Escribir "Descuento aplicado - IEES: $ ",desc
	Escribir "Neto a recibir: $ ",nrecibir
Fin Funcion
//presenta el mayor
funcion mayor_menor(num1,num2,num3)// ejercicio 4 y ejercicio 5
	si num1>num2 y num1>num3 entonces 
		escribir "el numero mayor es ",num1
	sino 
		si num2>num1 y num2>num3 Entonces
			escribir "el numero mayor es ",num2
		sino 
			escribir "el numero mayor es ",num3
		FinSi
	FinSi
FinFuncion

//f-4 Calcula y presenta si un estudiante ha aprobado la materia
funcion notas_promedio(num1,num2)
	suma= (num1+num2)/2
	si suma>=70 Entonces
		escribir "aprobado"
	SiNo
		si suma<70
			escribir "reprobado"
			
		FinSi
	FinSi
FinFuncion
//f-5 VALIDA si un numero es positivo o negativo
Funcion npositivo <- npnn(num)
	num=num
	Definir npositivo Como Entero
	Si num>0 Entonces
		npositivo=1
	SiNo
		Si num<0 Entonces
			npositivo=0
		FinSi
	FinSi
Fin Funcion
//f-6 Calcula si un numero es par o impar
Funcion numpar <-npar (num1,num2)
	Definir numpar Como Entero
	Si (num1 MOD num2=0) Entonces
		numpar=1
	SiNo
		numpar=0
	FinSi
Fin Funcion
//f-7 Calcula el mayor de una serie de 3 numeros
funcion nmayor=mumayor(num1,num2,num3)
	si num1>num2 y num1>num3 entonces 
		nmayor=num1
	sino 
		si num2>num1 y num2>num3 Entonces
			nmayor=num2
		sino 
			nmayor=num3
		FinSi
	FinSi
FinFuncion
//f-8 calcula el menor de 2 numeros
funcion nmenor=numenor(num1,num2)// ejercicio 4 y ejercicio 5
	si num1<num2 entonces 
		nmenor=num1
	sino 
		nmenor=num2
	FinSi
FinFuncion
//9-Calcula numero mayor de una secuencia dada
Funcion Mayor_menor_secuencia <- secmaymen (ct)
	i=1
	mientras i <= ct Hacer
		escribir " ingrese el numero ", i ;leer n
		si i=1 Entonces
			may=n
			men=n
		SiNo
			si n>may Entonces
				may=n
			FinSi
			si n<men Entonces
				men=n
			FinSi
		FinSi
		i=i+1
	FinMientras
	Escribir " El numero mayor es " " " may
	Escribir " Eel numero menor es " " " men
Fin Funcion
//f10-secuencia para numero 2
Funcion numpares <- secuencias ( i,n )//15
	Para i<-2 Hasta n Con Paso i Hacer
		Escribir i
	Fin Para
Fin Funcion
//f11- calcula promedios definidos por limites
Funcion promedios <- prome (sec)//17
	c=0;x=0 ;prom=0;nc=0
	Mientras c<sec Hacer
		Escribir " escriba un numero" Sin Saltar; Leer num
		Si num>=100 y num<=500 Entonces
			c=c+1; x=x+num ;nc=nc+1;prom=x/nc
		SiNo
			c=c+1
		Fin Si
	Fin Mientras
	Escribir "el promedio de los numeros es :" prom
Fin Funcion
//f12- numeros serie factorial
Funcion factorial <- numfac (n)
	fac=1
	Para f=1 Hasta n Con Paso 1 Hacer
		fac=fac*f
	Fin Para
	escribir "el factorial del numero ",n," es: ",fac; Escribir ""
Fin Funcion
// ******EJERCICIOS DE RAZONAMIENTO SIN USAR CADENAS Y VECTORES*******
//f1 numeros serie factorial
Funcion ndigitos<- digitos (n)
	contador=0
	Mientras n>0 Hacer
        n=trunc(n/10)
        contador=contador+1
    Fin Mientras
    Escribir "el numero que escribio tiene ",contador," ","digitos"
Fin Funcion
//f2-convierte un numero binario a decimal
Funcion nbinario <- binario (num)
	potencia=0;acu=0;residuo=0;enter=0;calexponente=0;multiplicacion=0
	Mientras num>0 Hacer
		residuo=num mod 10
		enter=trunc	(num/10)
		num=enter
		calexponente= 2^potencia
		multiplicacion=calexponente*residuo
		acu=acu+multiplicacion
		potencia=potencia+1
	Fin Mientras
	Escribir "El numero en base 10 es:", acu
Fin Funcion
//f-3 convierte un binario a hexadecimal
Funcion Bin_Oc_Hex <- transformador ( num, base )//3 sin vectores
	Segun base Hacer
		Caso "B" o "b":
			si num=0 Entonces
				conv=0
			SiNo
				mientras num>0 Hacer
					mo= num%2
					num=trunc(num/2)
					si mo>0 Entonces
						conv=1
					SiNo
						conv=0
					FinSi
					Escribir "bit ",x,":  ",conv
					x=x+1
				FinMientras
				
			FinSi
		Caso "O" o "o":
			si num=0 Entonces
				conv=0
			SiNo
				Repetir
					mo= num%8
					conv=conv+10^x*mo
					x=x+1
					num=trunc(num/8)
			    Hasta Que num<8
				b=conv+10^x*num
				Escribir "Su transformacion al sistema Octal es: ",b
			FinSi
		caso "H" o "h":	
			x=1
			q=""
			Mientras x>0 Hacer
				x=trunc(num/16)
				Segun num%16 hacer
					Caso 10:
						
						q="A"+q
					Caso 11:
						
						q="B"+q
					Caso 12:
						
						q="C"+q
				    caso 13:
						
						q="D"+q
					Caso 14:
						q="E"+q
					Caso 15:
						q="F"+q
					De Otro Modo:
						q=ConvertirATexto(num%16)+q
				FinSegun
				num=x
				Escribir "Su transformacion al sistema Hexadecimal es: ",q
			FinMientras
	FinSegun
Fin Funcion
//f-4 serie fibonacci
Funcion SERIE_FIBONIACCI <- serie ( num )//4 sin vectores
	a=0;b=1
	Para i=1 hasta num hacer 
		Escribir a
		c=a+b
		a=b
		b=c
	FinPara
Fin Funcion
//f-5 calcular divisores
Funcion divisores <- numdivisores (n) //5 sin vectores
	Para i<-1 Hasta n Con Paso 1 Hacer
		Si n mod i=0 Entonces
			Escribir i
		Fin Si
	Fin Para
Fin Funcion
//f-6 calcular si el numero es perfecto
Funcion perfecto <- numperfecto (n)
	c=1;sum=0
	Mientras c<n Hacer
		Si n % c=0 Entonces
			sum=sum+c
		Fin Si
		c=c+1
	Fin Mientras
	Si sum=n Entonces
		Escribir "el numero  ",n,"  es perfecto"
	SiNo
		Escribir "el numero  ",n,"  no es perfecto"
	Fin Si
Fin Funcion
// f-7 calcula los divisores de un numero
Funcion acu <- divisor(num)	//8 sin vectores	
	Definir acu Como real	
	acu=0
	Para c<-1 Hasta num-1 Hacer
		Si (num mod c)=0 Entonces
			acu=acu+c
		FinSi
	FinPara
FinFuncion

// f-8 Verifica si un numero es primo o no y devueve 1 si es primo y 0 si no es
Funcion bandera=primo(num)
	Definir r,c,bandera Como Entero
	bandera=1;c=2
	// recorro mientras sea primo y c no lleaga al numero
	Mientras bandera=1 y c < num Hacer
		r=num MOD c
		Si r=0 Entonces
			bandera=0
		SiNo
			c=c+1
		Fin Si
	Fin Mientras
	
FinFuncion
// f-9 Recorrer y presentar cadena caracter por caracter
Funcion presentarCadena(texto)
	Definir pos,lon Como Entero
    //Escribir Subcadena(texto,2,3)
	lon = Longitud(texto)
	Para pos=1 Hasta lon  Hacer
		Escribir Subcadena(texto,pos,pos)
	Fin Para
FinFuncion
//f-10 recorre y cadena y cuenta las vocales de dicha cadena
Funcion vocales(texto)
	Definir pos,lon Como Entero
	Definir reves Como Caracter;reves=" ";cv=0
    //Escribir Subcadena(texto,2,3)
	lon = Longitud(texto)
	Para pos=1 Hasta lon  Hacer
		Si Subcadena(texto,pos,pos) = 'a' | Subcadena(texto,pos,pos)='e' | Subcadena(texto,pos,pos)='i' Entonces
			cv = cv +1
		Fin Si
		reves=Subcadena(texto,pos,pos)+reves
	Fin Para
    Escribir "Frase al revez: ",reves,"  No vocales: ",cv
FinFuncion
//f-11 Ingresar datos a un arreglo
Funcion ingresarArreglo(vector por referencia,lim)
	Definir num,pos Como Entero
	Para pos<-1 Hasta lim Hacer
		Escribir "Ingrese numero"
		leer num
		vector[pos]=num
	Fin Para
FinFuncion
//f-12 Presenta los datos de un arreglo dada una posicion inicial y final
Funcion presentarArreglo(vector,inicio,lim)
	Definir pos Como Entero
	Si lim > 0 Entonces
		Para pos<-inicio Hasta lim Hacer
			Escribir pos,"[",vector[pos],"]"
		Fin Para
	SiNo
		Escribir "Ingrese primero datos al arreglo"
	Fin Si
FinFuncion
//f-13 buscac un valor en un arreglo y retorna la posicion si lo encuentra sino retorna -1
Funcion posicion=buscarArreglo(buscado,vector,lim)
	definir pos,encontrado Como Entero
	pos=1;encontrado=0
	mientras encontrado=0 y pos <= lim Hacer
		Si vector[pos]=buscado Entonces
			encontrado=1
		SiNo
			pos = pos + 1
		Fin Si
	Fin Mientras
	Si encontrado=1 Entonces
		posicion=pos
	SiNo
		posicion = -1
	Fin Si
FinFuncion
//f14-de una serie me da los numeros primos
Funcion nsprimos<-pserie(nserie,lon)
	Dimension arreglo1(15)
	temporal=nserie
	np=0
	cont=0
	Para k<-1 Hasta lon Hacer
		Mientras temporal>0 Hacer
			digito=temporal mod 10
			temporal=(temporal-digito)/10
			Si primo(digito)=1 Entonces
				cont=cont+1
				arreglo1(15)=ConvertirATexto(digito)
				Escribir "Los numeros primos en la serie son ",arreglo1(15)
			Fin Si
		Fin Mientras
	Fin Para
FinFuncion
//f-15-promedio de notas mayores a 70
Funcion promedio=pnotas(arreglo,lim)
	promedio=0;suma=0;cont=0
	Para k<-1 Hasta lim Hacer
		Si arreglo[k]>=70 Entonces
			suma=arreglo[k]+suma
			cont=cont+1
		Fin Si
		promedio=suma/cont
	Fin Para
	Escribir "La suma de las calificaciones ingresadas es: ", suma
	Escribir "Las notas ingresadas dan un promedio de: ", promedio
FinFuncion
//f16-lee la cadena y la presenta invertida
Funcion presentarcadenainvertida(texto)
	Definir pos,lon Como Entero
	lon = Longitud(texto)
	Para pos=lon Hasta 1 con paso -1 Hacer
		Escribir Subcadena(texto,pos,pos)
	Fin Para
FinFuncion
// f-17-cuenta palabras de una cadena
Funcion npalabras(texto)
	Definir pos,lon,cont Como Entero
	cont=0
	lon = Longitud(texto)
	Para pos=1 Hasta lon  Hacer
		Si Subcadena(texto,pos,pos) = " "
			cont = cont +1
		Fin Si
	Fin Para
	cont=cont+1
    Escribir "Frases ingresadas fueron : ",cont
FinFuncion
//f-18 Buscar caracter 
Funcion posicion=vbuscado(buscado,frase,lim)
	posicion=0;acu=0
	Para i<-1 hasta lim Hacer
		verificador=Subcadena(frase,i,i)
		SI verificador= buscado Entonces
			acu=acu+1
			posicion=i
		FinSi
	FinPara
	SI posicion <> 0 Entonces
		Escribir "Se encontro la frase en la posicion: ",acu,"--",posicion
	SiNo
		posicion=-1
		Escribir " ", posicion
	FinSi
FinFuncion
//f-19 Comparar caracteres 
Funcion caracteres_igual=caracteres_iguales(p1,p2)
	arreglo1=p1; arreglo2=p2
	si arreglo1=arreglo2 Entonces
		Escribir "los caracteres son Iguales"
	SiNo
		Escribir "los caracteres son Diferentes"
	FinSi
FinFuncion
//f-20 funcion palindroma
funcion Palindromo = npalindromo(lim,palabra)
	i=1
	Mientras i < lim	& Minusculas(Subcadena(palabra, i, i)) = Minusculas(Subcadena(palabra,lim,lim)) Hacer
		i=i+1
		lim=lim-1
	FinMientras
	Escribir ""
	Si i>=lim Entonces
		Escribir " La palabra ", palabra," es palíndroma."
	Sino
		Escribir "La palabra ingresada no es palindroma"
	FinSi
FinFuncion
// Algoritmo principal
Algoritmo Menu
	Definir pos,resp,limite,dato Como Entero
	Definir opcion,opc1,opc2,opc3,frase Como Caracter
	Dimension menuPrincipal[4],menuNumeros[21],menuRazonamiento[11],menuCadenaVectores[16]
    Dimension arreglo[100]	
	limite=0
	// ***** MENU PRINCIPAL ******
	menuPrincipal[1] = "1)Ejercicios Basicos"
	menuPrincipal[2] = "2)Ejercicios Razonamiento"
	menuPrincipal[3] = "3)Ejercicios Cadenas y Vectores"
	menuPrincipal[4] = "4)Salir"
	
	// ***** EJERCICIOS BASICOS ******
	// dado 2 numeros presentar la suma
	menuNumeros[1] = "1)Sumar dos numeros"
	// dado 2 numeros si el primero es  >= al segundo sumarlo sino restarlo
	menuNumeros[2] = "2)Sumar o restar"
	// ingrese 2 numeros con una operacion matematica("+,-,*,/,%,^") 
	// realizar y presentar la respuesta de la operacion matematica
	menuNumeros[3] = "3)Caculadora"
	// presentar el mayor de 2 numeros ingresados
	menuNumeros[4] = "4)Mayor de dos numeros"
	// presentar el menor de 3 numeros ingresados
	menuNumeros[5] = "5)Mayor de tres numeros"
	// La despensa "El Baraton", a todas las ventas que pasen de $100,
	//se les aplicará un  descuento del 10%, a  todo los demás se les aplicará sólo el 5% 
	// luego del recargo del iva del 12%
	menuNumeros[6] = "6)Comprar productos"
	// Ingresar el nombre,horas trabajadas, valor hora, horas50 y horas100 de sobretiempo
	// se pide calcular el rol del pago del trabajor dado los siguientes calculos.
	// sueldo del trabajador(horasTrajabadas por el valorHora)
	// el sobretiempo=(horas50*1.5+horas100*2)*Valor
	// total ingreos = sueldo+sobretiempo
	// descuento= totalingresos*9.35%
	// liquido= totalingresos - descuento
	menuNumeros[7] = "7)Pago de Sueldos"
	// dada dos notas calcular el promedio y presentar el mensaja "Aproboado" si el promedio
	// mayor 70 y reprobado si es menor que 70
	menuNumeros[8] = "8)Notas de Alumnos"
	// dado un numero indicar si es positivo o negativo
	menuNumeros[9] = "9)Positivo/Negativo"
	// dado un numero indicar si es par o impar
	menuNumeros[10] = "10)Par e Impar"
	// dado dos nmeros indicar si el numero1 es multiplo del numero2
	menuNumeros[11] = "11)Multiplo de cualquier Numero"
	// dada una secuencia de numeros presentar el mayor y el menor de esa esa secuencia
	menuNumeros[12] = "12)El Mayor y Menor de una secuencia de numeros"
	// dada una secuencia de numeros presentar cuantos son numeros positivos
	menuNumeros[13] = "13)Positivos de una secuencia de numeros"
	// dada una serie d enumeros presntar la suma de lo numeros multiplos de 5
	menuNumeros[14] = "14)Suma de los multiplos de cinco de una serie de numeros"
	// presentar los numeros pares desde 2 hasta N
	menuNumeros[15] = "15)Generar y presentar los Numeros pares del 2 a N"
	// dada una serie de numeros presente el numero siempre y cuando sea negativo
	// y antes de finalizar presente la cantidad, la suma y el promedio de dichos numeros negativos
	menuNumeros[16] = "16)Cantidad, Suma y Promedio de numeros negativos de una serie"
	// dado una serie de numeros presentar el promedio de los numeros que sean >= 100 and <=500 
	menuNumeros[17] = "17)Serie promedio de rango de numeros"
	// Ingrese un numero si este es postivo finalice sino vuelva a ingresarlo
	menuNumeros[18] = "18)Validar que un numero sea positivo"
	// dado dos numeros base y exponente. calcular la base elevada al exponente 
	menuNumeros[19] = "19)Base y exponente"
	// dada una serie de numeros calcular los factorles de dichos numero la serie termina
	// cuando un numero de la serie sea igual a cero.
	menuNumeros[20] = "20)Serie Factoriales"
	menuNumeros[21] = "21)Salir"
	
	// ******EJERCICIOS DE RAZONAMIENTO SIN USAR CADENAS Y VECTORES*******
	// contar los digitos de cualquier numero: ej: 342 = 3 digitos
	menuRazonamiento[1] = "1)Cuantos Digitos tiene un Numero"
	// ejemplo: binario= 1111101000  ==> 1000 (decimal) Respuesta = 3E8 (hexadecimal)
	menuRazonamiento[2] = "2)Covertir de Base 2 a Base 10"
	// ejemplo: binario= 1100100  ==> 100 (decimal) => 
	menuRazonamiento[3] = "3)Convertir de Base 2 a Base 16 pasando por Base 10"
	// ejemplo Si n=8 => La serie de fibonacci es = 0 1 1 2 3 5 8 13
	menuRazonamiento[4] = "4)Serie de fibonacci hasta N"
	// ejemplo si numero=10 ==> resp= 1,2,5
	menuRazonamiento[5] = "5)Divisores de un Numero"
	// cuando los divisores de un numero es igual al numero se dice que ese numero es perfecto
	// si numero es 6 los divisores son 1,2,3 sumados igual a 6 es igual al numero ==> es perfecto
	menuRazonamiento[6] = "6)Numero Perfecto"
	// un numero es primo cuando no tiene divisores excepto el 1 y el propio numero
	// ejemplo 5 no tiene divsores es "primo"  9 tiene como divisor al 3 por tanto no es "primo"
	menuRazonamiento[7] = "7)Numero Primo"
	// dos numeros son amigos si la suma de los divisores del primer numero es = a la suma de los
	// divisores del segundo numero Eje: numero=6 (1+2+3)=6 numero 25(1+5)=6 ==> 6=6 son amigos 
	menuRazonamiento[8] = "8)Verificar si dos numeros son Amigos"
	// dos numeros son primos gemelos si ambos numeros son primos en valor absoluto y la resta de
	// los 2 numeros es == 2 eje: 11 y 13 abs(11-13)==2
	menuRazonamiento[9] = "9)Verificar si dos Numeros son Primos Gemelos"
	// por cada numero de una serie verifique si es primo, si es primo contarlo
	menuRazonamiento[10] = "10)De una serie de numeros cuantos son Primos"
	menuRazonamiento[11] = "11)Salir"
	
	// ****** EJERCICIOS DE CADENAS Y VECTORES *******
	// dado n valores ingresarlos a un arreglo
	menuCadenaVectores[1] = "1)llenar un arreglo de numeros"
	// recorrer un arreglo y presentarlo uno por uno
	menuCadenaVectores[2] = "2)Presentar los elementos de un arreglo"
	// buscar el dato y presentar la posicion en que se encuntra el dato en el arreglo
	menuCadenaVectores[3] = "3)Buscar un dato en un arreglo"
	// Presentar el mayor de los datos de un arreglo
	menuCadenaVectores[4] = "4)Elemento Mayor de un arreglo"
	// Dado un arreglo copie los datos en otro de atras para delante 
	menuCadenaVectores[5] = "5)Copiar los datos de un arreglo en otro invertido"
	// Dado los arreglos1 y arreglos 2 sumarlos valor por valor del arreglo1 y arreglo2
	// y asigmarlo en el arreglo3(los tres arreglos deben tener la misma longitud)
	menuCadenaVectores[6] = "6)Dados los arreglos arreglo1[] y arreglo2[] Sumarlos en al arreglo Suma[]"
	// de una serie de numeros solo guardar en un arreglo los nymeros primos
	menuCadenaVectores[7] = "7)Dado N numeros guardar en un arreglo los numeros primos"
	// dadas las notas guardades en un arreglos obtener el promedio de las notas >= 70
	menuCadenaVectores[8] = "8)Dadas los datos de un arreglo obtener el promedio de las notas >=> 70"
	// presentar caracter por caracter de una cadena
	menuCadenaVectores[9] = "9)Dada una cadena presentarla caracter por caracter"
	// presentar  una cadena al reves
	menuCadenaVectores[10] = "10)Dada una cadena presentarla invertida"
	// contar las vocales de jque tiene una cadena cualquiera
	menuCadenaVectores[11] = "11)Dada una cadena indicar cuantos vocales tiene"
	// contar las palabras de una cadena
	menuCadenaVectores[12] = "12)Dada una cadena indicar cuantos palabras tiene"
	// buscar si un caracteer ingresado se encuentra en una cadena si es asi presentarMenu
	// la posicion en que se encuntra ese caracter en la cedena caso contrario presentar -1
	menuCadenaVectores[13] = "13)presentar la posicion de un caracter buscado dentro de una cadena"
	// comparar 2 cadenas csracter por catacter e indicar si son iguales o no
	menuCadenaVectores[14] = "14)Dadas dos cadenas indicar si son iguales caracter por caracter"
	// una cadena es palaindorma si se lee de la misma forma de izquierda a derecha 
	// ejemplo "ana"
	menuCadenaVectores[15] = "15)Indicar si una cadena es palindroma"
	menuCadenaVectores[16] = "16)Salir"
	// cuando se escoja la opcion 4 termina el programa
	opcion=''
	Mientras opcion <> "4" Hacer
		Borrar Pantalla
		opcion = presentarMenu("********** M E N U   P R I N C I P A L **********",menuPrincipal,4)
		Borrar Pantalla
		Segun opcion Hacer
			"1":
				opc1=""
				Mientras opc1<>"21" Hacer
					opc1= presentarMenu("********** M E N U  E J E R C I C I O S  B A S I C O S **********",menuNumeros,21)
					Borrar Pantalla
					Segun opc1 Hacer
						"1":// dado 2 numeros presentar la suma
							Escribir "Sumar Dos  Numeros"
							escribir "ingrese un numero";leer num1
							escribir"ingrese un numero";leer num2
							calculadora("+",num1,num2)
							Escribir "espere por favor ,regresando al Menu ..."
							Borrar Pantalla
							Esperar 3 Segundos
						"2":// dado 2 numeros si el primero es  >= al segundo sumarlo sino restarlo
							escribir "sumar o restar"
							escribir "ingrese un numero1"
							leer numero1
							escribir "ingrese un numero2"
							leer numero2
							si numero1>=numero2
								calculadora("+",numero1,numero2)
							SiNo
								calculadora("-",numero1,numero2)
							FinSi
							Escribir "espere por favor ,regresando al Menu ..."
							esperar 3 Segundos
							Borrar Pantalla
						"3":// ingrese 2 numeros con una operacion matematica("+,-,*,/,%,^") 
							// realizar y presentar la respuesta de la operacion matematica
							Escribir "Calculadora"
							Escribir "Ingrese Operacion[+,-,*,/,%,^]: " Sin Saltar
							Leer ope
							Escribir "Ingrese Numero1: " Sin Saltar
							Leer num1
							Escribir "Ingrese Numero2: " Sin Saltar
							Leer num2
							calculadora(ope,num1,num2)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							Borrar Pantalla
						"4":// presentar el mayor de 2 numeros ingresados 
							Escribir "Mayor de dos numeros"; Escribir "  "
							escribir "ingrese el numero 1" Sin Saltar;leer num1
							escribir "ingrese el numero 2 " Sin Saltar;leer num2
							mayor_menor(num1,num2,0)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							Borrar Pantalla
						"5":// presentar el mayor de 3 numeros ingresados
							escribir "Mayor de 3 numeros"
							escribir "ingrese el numero 1"Sin Saltar;leer num1
							escribir "ingrese el numero 2"Sin Saltar;leer num2
							escribir "ingrese el numero 3"Sin Saltar;leer num3
							mayor_menor(num1,num2,num3)
							escribir "espere por favor , regresando al Menu ..."
							esperar 3 Segundos
							Borrar Pantalla
						"6":// La despensa "El Baraton", a todas las ventas que pasen de $100,
							//se les aplicará un  descuento del 10%, a  todo los demás se les aplicará sólo el 5% 
							// luego del recargo del iva del 12%
							Escribir "Comprar productos el baraton "
							Escribir "Ingrese la compra" Sin Saltar;Leer ventas
							Descuentos = descuento(0.12,ventas)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 6 Segundos
							Borrar Pantalla
						"7":// se pide calcular el rol del pago del trabajor dado los siguientes calculos.
							Escribir "Pago de Sueldos"; Escribir  " "
							Definir horas,vhora,hextra,hcien,vtotal,desc Como Real
							Definir nom Como caracter	
							Escribir "ingrese el nombre completo del empleado" Sin Saltar; Leer nom
							Escribir "ingrese el numero de horas trabajadas" Sin Saltar;leer horas
							Escribir "ingrese el valor de horas trabajadas" Sin Saltar;leer vhora
							Escribir "ingrese el numeros de horas trabajadas al 50%" Sin Saltar; Leer hextra
							Escribir "ingrese el numeros de horas trabajadas al 100%" Sin Saltar; Leer hcien
							Pago_de_sueldo <- pago ( horas,vhora,hextra,hcien,nom)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 6 Segundos
							Borrar Pantalla
						"8":// dada dos notas calcular el promedio y presentar el mensaja "Aproboado" "reprobado"
							Escribir "Notas de Alumnos"; Escribir "  "
							Escribir "ingrese la nota del primer parcial"Sin Saltar;leer primer
							Escribir "ingrese la nota del segundo parcial"Sin Saltar;Leer segundot
							notas_promedio(primer,segundot)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							Borrar Pantalla
						"9":// dado un numero indicar si es positivo o negativo
							Escribir "Positivo/Negativo"; Escribir "  "
							Escribir "Digita un numero: ";leer	num
							resp=npnn(num)
							si resp=1 Entonces
								Escribir "El numero: ",num, " Es positivo"
							SiNo
								Si resp=0 & num<>0 Entonces
									Escribir "El numero: ",num, " Es Negativo"
								SiNo
									Escribir num, " es neutro"
								Fin Si
							FinSi
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							Borrar Pantalla	
						"10":// dado un numero indicar si es par o impar
							Escribir "Par e Impar"; Escribir "  "
							Escribir "escriba un numero para determinar si es par o imprar"
							leer num1
							resp=npar(num1,2)
							Si resp=1 Entonces
								Escribir "El numero ",num1," es Par"
							SiNo
								Escribir "El numero",num1,"es impar'
							Fin Si
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							Borrar Pantalla
						"11"://dado dos nmeros indicar si el numero1 es multiplo del numero2
							Escribir "Multiplos- dos nmeros indicar si el numero1 es multiplo del numero2"; Escribir "  "
							Escribir "Ingrese un numero " ;leer num1
							Escribir "Ingrese un numero " ;leer num2
							j=mumayor(num1,num2,0)
							k=numenor(num1,num2)
							resp=npar(j,k)
							Si resp=1 Entonces
								Escribir "El numero ", num1," es multiplo del numero ", num2
							SiNo
								Escribir "el numero",num1," no es multiplo del numero ",num2 
							Fin Si
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"12":// dada una secuencia de numeros presentar el mayor y el menor de esa esa secuencia
							Escribir "El Mayor y Menor de una secuencia de numeros";  Escribir " "
							may=0
							men=0
							Escribir "ingrese la cantidad de numeros que quiere en su secuencia"
							leer ct
							Mayor_menor_secuencia <- secmaymen ( ct )
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"13":// dada una secuencia de numeros presentar cuantos son numeros positivos
							Escribir "Positivos de una secuencia de numeros"; Escribir "  "
							Escribir "ingrese el numero de la secuencia"; sin saltar;leer sec
							cont=0;resp=0;contnp=0;np=0
							Mientras cont<sec Hacer
								Escribir "ingrese un numero" Sin Saltar;leer num
								resp=npnn(num)
								si resp=1 Entonces
									contnp=contnp+1
									np=num
								FinSi
								cont=cont+1
							FinMientras
							Escribir "hay ",contnp," numeros positivos"
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"14":// dada una serie d enumeros presntar la suma de lo numeros multiplos de 5
							Escribir "Suma de los multiplos de cinco de una serie de numeros"; Escribir " "
							Escribir "ingrese la secuencia" Sin Saltar
							Leer sec;suma=0
							Para i<-1 Hasta sec Hacer
								Escribir "Ingrese valores";Leer num1
								resp=npar(num1,5)
								si resp=1 Entonces
									suma=num1+suma
								FinSi
							Fin Para
							Escribir "La suma de los multiplos de 5 encontrados es: ",suma
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"15":// presentar los numeros pares desde 2 hasta N
							Escribir "Generar y presentar los Numeros pares del 2 a N" ; Escribir "  "
							Escribir " ingrese un numero" Sin Saltar; leer n;i=2
							numpares <- secuencias ( i,n)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 5 Segundos
							Borrar Pantalla
						"16":// dada una serie de numeros presente el numero siempre y cuando sea negativo
							// y antes de finalizar presente la cantidad, la suma y el promedio de dichos numeros negativos
							Escribir "Contar y calcular suma - promedio de numeros negativos"
							Escribir "ingrese el numero de digitos a evaluar" Sin Saltar; Leer sec
							cont=0;suma=0;promedio=0
							Para i<-1 Hasta sec Hacer
								Escribir "ingrese un numero" Sin Saltar; Leer num
								Si npnn(num)=0 Entonces
									cont=cont+1
									suma=suma+num
									promedio=suma/cont
								Fin Si
							Fin Para
							Escribir "Hay ", cont, " numeros negativos ingresados"
							Escribir "La suma es: ", suma
							Escribir "El promedio de los numeros negativos es: ",promedio
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 6 Segundos
							Borrar Pantalla
						"17":// dado una serie de numeros presentar el promedio de los numeros que sean >= 100 and <=500 
							Escribir "Serie promedio de rango de numeros que sean >= 100 and <=500"; Escribir " "
							Escribir "ingrese numero de secuencia" Sin Saltar; leer sec
							promedios <- prome (sec)
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"18":// Ingrese un numero si este es postivo finalice sino vuelva a ingresarlo
							Escribir "Validar que un numero sea positivo"; Escribir " "
							Escribir "ingrese un numero" Sin Saltar; Leer num
							Mientras npnn(num)=0 Hacer
								Escribir "Favor ingresar nuevamente un numero "
								Escribir "E	l numero ingresado es invalido"
								Escribir "ingrese un numero" Sin Saltar; Leer num
							Fin Mientras
							Escribir "El numero ingresado es positivo ",num
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"19":// dado dos numeros base y exponente. calcular la base elevada al exponente 
							Escribir "Base y exponente"; Escribir " "
							Escribir "ingrese el numero de la base" sin saltar ; leer b
							Escribir "ingrese un numero al exponente"Sin Saltar; leer e
							calculadora("^",b,e)
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"20":// dada una serie de numeros calcular los factorles de dichos numero la serie termina
							// cuando un numero de la serie sea igual a cero.
							Escribir "Serie Factoriales - proceso finalizara cuando ingrese 0"; Escribir " "
							n=1
							Mientras n<>0 Hacer
								Escribir "ingrese un numero a calcular" Sin Saltar; Leer n
								Si n<>0 Entonces
									factorial <- numfac (n)
								SiNo
									Escribir "se ah ingresado un 0 ,PROCESO FINALIZADO"
								Fin Si
							Fin Mientras
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos
							Borrar Pantalla
						"21":
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos
							
						De Otro Modo:
							Escribir "Opcion Incorrecta"
							Esperar 3 Segundos
					Fin Segun
				Fin Mientras
			"2":
				opc2=""
				Mientras opc2<>"11" Hacer
					opc2= presentarMenu("********** M E N U  E J E R C I C I O S  D E  RAZONAMIENTO **********",menuRazonamiento,11)
					Borrar Pantalla
					Segun opc2 Hacer
						"1":
							Escribir "Cantidad de Digitos de un Numero"; Escribir "  "
							Escribir "Ingrese un numero"
							Leer n
							Numeros_En_Digitos <- digitos ( n )
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"2":// ejemplo: binario= 1100100  ==> 100 (decimal) => 
							Escribir "Covertir de Base 2 a Base 10";  Escribir "  "
							Escribir "Ingrese un numero binario"Sin Saltar; Leer num
							t=0;acu=0;v=0;r=0
							nbinario <- binario ( num )
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"3":// ejemplo: binario= 1111101000  ==> 1000 (decimal) Respuesta = 3E8 (hexadecimal)
							Escribir "Convertir de Base 2 a Base 16 pasando por Base 10"; Escribir " "
							Escribir "ingrese el numero a transformar"
							Leer num
							Escribir "escoja la base a la que desea convertir el nunero (H=hexadecimal)"
							Leer base
							Bin_Oc_Hex <- transformador ( num, "h" )
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"4":// ejemplo Si n=8 => La serie de fibonacci es = 0 1 1 2 3 5 8 13
							Escribir "Serie de fibonacci hasta N"; Escribir "  "
							Escribir "ingrese un numero por favor"
							leer num
							SERIE_FIBONIACCI <- serie ( num )
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"5":// ejemplo si numero=10 ==> resp= 1,2,5
							Escribir "Divisores de un Numero"; Escribir " "
							Escribir "ingrese un numero" Sin Saltar; leer n
							divisores <- numdivisores( n)
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"6":// cuando los divisores de un numero es igual al numero se dice que ese numero es perfecto
							// si numero es 6 los divisores son 1,2,3 sumados igual a 6 es igual al numero ==> es perfecto
							Escribir "[Numero Perfecto]";Escribir " "
							Escribir "ingrese un numero"
							leer n
							perfecto <- numperfecto ( n)
							
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"7":// un numero es primo cuando no tiene divisores excepto el 1 y el propio numero
							// ejemplo 5 no tiene divsores es "primo"  9 tiene como divisor al 3 por tanto no es "primo"
							Escribir "Ejercicio Numero Primo"
							Escribir "Ingrese Numero: " Sin Saltar
							Leer num
							resp=primo(num)
							Si resp=1 Entonces
								Escribir num," Es Primo"
							SiNo
								Escribir num," No Es Primo"
							Fin Si
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 2 Segundos
							Borrar Pantalla
						"8":// dos numeros son amigos si la suma de los divisores del primer numero es = a la suma de los
							// divisores del segundo numero Eje: numero=6 (1+2+3)=6 numero 25(1+5)=6 ==> 6=6 son amigos
							Escribir "Verificar si dos numeros son Amigos" ; Escribir "  "
							Escribir " Ingrese dos numeros"
							Leer num1,num2
							si divisor(num1)=num2 Y divisor(num2)=num1 Entonces
								Escribir " Los numeros ",num1, " y ",num2," son amigos"
							siNo
								Escribir " Los numeros ",num1, " y ",num2," no son amigos"
							Fin Si
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos
							Borrar Pantalla
						"9":// dos numeros son primos gemelos si ambos numeros son primos en valor absoluto y la resta de
							// los 2 numeros es == 2 eje: 11 y 13 abs(11-13)==2
							Escribir "Ejercicio Primos Gemelos"; Escribir "  "
							Escribir "Ingrese Numero1: " Sin Saltar
							Leer num1
							Escribir "Ingrese Numero2: " Sin Saltar
							Leer num2
							Si primo(num1)=1 y primo(num2)=1 y abs(num1-num2)=2 Entonces
								Escribir num1," y ",num2," Son Primos Gemelos"
							SiNo
								Escribir num1," y ",num2," No Son Primos Gemelos"
							Fin Si
							resp=primo(num)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 2 Segundos
							Limpiar Pantalla
						"10":// por cada numero de una serie verifique si es primo, si es primo contarlo
							escribir "de una serie de numeros cuantos son primos"
							seriesh=0
							temporal=seriesh
							cont=0
							escribir "ingrese el limite de la serie "
							leer limite 
							para i=1 hasta limite con paso 1 Hacer
								escribir "ingrese una serie de numeros"
								leer seriesh
								digito= seriesh mod 10
								temporal=(temporal-digito)/10
								si primo(digito)=1 entonces
									cont = cont+1
								FinSi
							FinPara
							escribir "en la serie ingresada hay ",cont," numeros primos "
							Escribir "Espere por favor, Regresando Al Menu..."
							esperar 3 Segundos
							Limpiar Pantalla
						"11":
							Escribir "Regresando Al Menu Principal..."
							Esperar 1 Segundos
						De Otro Modo:
							Escribir "Opcion Incorrecta"
							Esperar 3 Segundos
					Fin Segun
				Fin Mientras
			"3":
				opc3=""
				Mientras opc3<>"16" Hacer
					opc3= presentarMenu("********** M E N U  E J E R C I C I O S  C A D E N A S  Y  V E C T O R E S **********",menuCadenaVectores,16)
					Borrar Pantalla
					Segun opc3 Hacer
						"1":// dado n valores ingresarlos a un arreglo
							Escribir "Ingresar Valores a un Arreglo"
							Escribir "Ingrese cantidad de Elementos:" Sin Saltar
							Leer limite
							ingresarArreglo(arreglo,limite)
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos
							Borrar Pantalla
						"2":// recorrer un arreglo y presentarlo uno por uno
							Escribir "Presentar los Valores de un Arreglo"
							presentarArreglo(arreglo,1,limite)
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos
							limpiar pantalla
						"3":// buscar el dato y presentar la posicion en que se encuntra el dato en el arreglo
							Escribir "Buscar un dato en un Arreglo"
							presentarArreglo(arreglo,1,limite)
							Escribir "Ingrese el dato a buscar en el arreglo"
							Leer dato
							pos = buscarArreglo(dato,arreglo,limite)
							Si pos > 0 Entonces
								Escribir dato," Se encuentra en la posicion: ",pos, " del arreglo"
							SiNo
								Escribir dato," No se encuentra en el arreglo"
							Fin Si
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos	
							Borrar Pantalla
						"4":// Presentar el mayor de los datos de un arreglo
							Escribir "Mayor de un arreglo"
							Escribir "Ingrese cantidad de Elementos:" Sin Saltar;Leer lim
							Escribir "Ingrese los datos para vector A"
							ingresarArreglo(arreglo,lim)
							mayore=0
							Para n<-1 Hasta lim Hacer
								Si arreglo[n] > mayore Entonces
									mayore = arreglo[n]
								Fin Si
							Fin Para
							Escribir "El numero mayor ingresado es: [", mayore,"]"
							Escribir "Regresando Al Menu Principal..."
							Esperar 3 Segundos
							Borrar Pantalla
						"5":// Dado un arreglo copie los datos en otro de atras para delante 
							Escribir "Arreglo inverso"
							Escribir "Ingrese cantidad de Elementos:" Sin Saltar;Leer lim
							Escribir "Ingrese los datos para vector A"
							ingresarArreglo(arreglo,lim)
							Para n<-lim Hasta 1 Con Paso -1 Hacer
								Escribir "el inverso del arreglo en la pos ",n," ingresado es ", arreglo[n]
							Fin Para
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"6":// Dado los arreglos1 y arreglos 2 sumarlos valor por valor del arreglo1 y arreglo2
							// y asigmarlo en el arreglo3(los tres arreglos deben tener la misma longitud)
							Escribir "Suma de vectores"
							Dimension arreglo1[15],arreglo2[15],sumarreglo[15]
							Escribir "Ingrese cantidad de Elementos:" Sin Saltar
							Leer dim
							Escribir "Ingrese los datos para vector A"
							ingresarArreglo(arreglo1,dim)
							Escribir "Ingrese los datos para vector B"
							ingresarArreglo(arreglo2,dim)
							Escribir "Suma de vectores A + B"
							Para k<-1 Hasta dim Hacer
								sumarreglo[k]=arreglo1[k]+arreglo2[k]
								Escribir "La suma del vector C en la posicion ", k," es: ", sumarreglo[k]
							Fin Para
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"7":// de una serie de numeros solo guardar en un arreglo los nymeros primos
							Escribir "Arreglo numeros primos"
							Escribir " Ingrese una serie de numeros"
							Leer nserie
							lon=Longitud(ConvertirATexto(nserie))
							nsprimos<-pserie(nserie,lon)
							Escribir "Regresando Al Menu Principal..."
							Esperar 4 Segundos
							Borrar Pantalla
						"8":// dadas las notas guardades en un arreglos obtener el promedio de las notas >= 70
							Escribir "Promedio de notas >=70"
							Escribir "Ingrese cantidad de Elementos:" Sin Saltar;Leer lim
							promedio=0;suma=0;cont=0
							Escribir "Ingrese las calificaciones"
							ingresarArreglo(arreglo,lim)
							promedio=pnotas(arreglo,lim)
							Esperar 3 Segundos
							Borrar Pantalla
						"9":// presentar caracter por caracter de una cadena
							Escribir "Presentar caracter por caracter"
							Escribir "Ingrese frase:" Sin Saltar
							Leer frase
							presentarCadena(frase)
							Esperar 4 Segundos
							Borrar Pantalla
						"10":// presentar  una cadena al reves
							Escribir "Cadena Inversa"
							Escribir "Ingrese frase:" Sin Saltar
							Leer frase
							lon = Longitud(frase)
							presentarcadenainvertida(frase)
							Esperar 4 Segundos
							Borrar Pantalla
						"11":// contar las vocales de jque tiene una cadena cualquiera
							Escribir "Contar vocales"
							Escribir "Ingrese frase:" Sin Saltar
							Leer frase
							vocales(frase)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 3 Segundos
							Borrar Pantalla
						"12":
							Escribir "Ingrese frase:" Sin Saltar
							Leer frase
							lon = Longitud(frase)
							npalabras(frase)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"13":// contar las palabras de una cadenam//
							//presentarMenum/m/na caso contrario presentar -1
							Escribir "Buscar dato en arreglo"
							Escribir "Ingrese un caracter:" Sin Saltar
							Leer frase
							lim= Longitud(frase)
							Escribir "Escriba la frase a buscar:";
							Leer buscado
							posicion=vbuscado(buscado,frase,lim)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"14":// comparar 2 cadenas csracter por catacter e indicar si son iguales o no
							Escribir "Comparativo de cadena"
							Escribir "Ingresa un caracter o palabra" Sin Saltar
							leer p1
							Escribir "Ingresa un caracter o palabra"Sin Saltar
							leer p2
							caracteres_igual = caracteres_iguales(p1,p2)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"15":// una cadena es palaindorma si se lee de la misma forma de izquierda a derecha 
							// ejemplo "ana"
							Escribir "Palindroma"
							Escribir "Ingrese una palabra:"
							Leer palabra
							lim=Longitud(palabra)
							Palindromo = npalindromo(lim,palabra)
							Escribir "Espere por favor, Regresando Al Menu..."
							Esperar 4 Segundos
							Borrar Pantalla
						"16":
							Escribir "Regresando Al Menu Principal..."
							Esperar 1 Segundos
						De Otro Modo:
							Escribir "Opcion Incorrecta"
							Esperar 3 Segundos
					Fin Segun
				Fin Mientras
			"4":
				Escribir "Gracias por usar el sistema"	
			De Otro Modo:
				Escribir "Opcion incorrecta"
		Fin Segun
    Fin Mientras
FinAlgoritmo
